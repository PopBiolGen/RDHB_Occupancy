# Point process abundance model in JAGS
model{
  #priors
  psi ~ dunif(0, 1) # probability of a DA colony being real
  r0 ~ dunif(500, x.max/2) # extent of the invasion
  alpha.det ~ dnorm(0, 1/4) # intercept for log-odds of detection
  beta.det ~ dnorm(0, 1/4) # slope of some linear effect on detection
  sigma ~ dunif(100, 1000) # parameter affecting spread of the density kernel
  
  # place ground 0
  g0[1, 1] ~ dunif(g0.x.min, g0.x.max) # ground zero x
  g0[1, 2] ~ dunif(g0.y.min, g0.y.max) # ground zero y
  
  for (jj in 1:JJ){ # step through potential colonies
    # place colonies
    c.j0[jj, 1] ~ dunif(x.min, x.max) # place initial colonies into X Y matrix
    c.j0[jj, 2] ~ dunif(y.min, y.max)
    
    # find which are within extent radius
      # Compute the pairwise distances
    in.ex[jj] <- step(r0 - sqrt((c.j0[jj, 1]-g0[1, 1])^2 + (c.j0[jj, 2]-g0[1, 2])^2))
    
    # which are within suitable habitat?
    in.hab[jj] <- mask.raster[trunc(c.j0[jj, 2] / raster.scale)+1, trunc(c.j0[jj, 1] / raster.scale)+1]
    
    # which of these da colonies are real?
    da[jj] ~ dbern(psi) # fraction of data augmented colonies
    
    z[jj] <- in.ex[jj]*da[jj]*in.hab[jj] # colonies that are real
  }
  
  for (ii in 1:II){ # step through survey sites and calculate likelihoods
    # probability of presence, from any colony
    for (jj in 1:JJ){
      # distance from colony to site
      d.ij[ii, jj] <- sqrt((s.0[ii, 1] - c.j0[jj, 1])^2 + (s.0[ii, 2] - c.j0[jj, 2])^2)
      # expected encounter rate from each colony
      lambda.ij[ii, jj] <- z[jj]*lambda.0*exp(-d.ij[ii, jj]^2/(2*sigma^2)) # real or not, and number
    }
    # sum of expected rate
    lambda.i[ii] <- sum(lambda.ij[ii,]) + 0.001 # avoiding zeros here avoids issues with initialisation of model.
    # probability of presence at site across all (real) colonies
    prob.pres.i[ii] <- 1 - exp(-lambda.i[ii]) # collapse poisson expectation to binary
    pres[ii] ~ dbern(prob.pres.i[ii])
    logit(det[ii]) <- alpha.det + beta.det * sur.lev.var[ii]
    obs.i[ii] ~ dbern(pres[ii] * det[ii])
  }
}